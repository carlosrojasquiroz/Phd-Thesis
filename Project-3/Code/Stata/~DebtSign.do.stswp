/*------------------------------------------------------------------------------
Project: 		Oil Supply News and Sovereign Default Risk
Author: 		Carlos Rojas Quiroz
Institution: 	European University Institute
Date: 			2024-06-25
Method: 		Local Projection Panel Data
Description: 	High/Low Debt Regimes (only negative shocks)
------------------------------------------------------------------------------*/
* Clear previous data and set options
clear all
cls
set more off
set graph off
graph set print fontface "Helvetica"
graph set window fontface "Helvetica"
* Housekeeping
global dir "/Users/carlosrojasquiroz/Desktop/EUI-Phd-Thesis/Project-3"
cd "$dir/Data"
*-------------------------------------------------------------------------------
* Section 1: Load data
*-------------------------------------------------------------------------------
import excel "DataBase_May2024.xlsx", sheet("Panel6") firstrow
replace quarter = qofd(quarter)
format quarter %tq
save DataPanel6.dta, replace
clear 
use DataPanel6.dta, clear
* Declaring the panel data
tsset code quarter
*-------------------------------------------------------------------------------
* Section 2: Local projection specifics
*-------------------------------------------------------------------------------
* Forecast horizon
local HOR = 12
* Lag order
local LAG = 4
* Shock sign
local SIGN = -1.0
* Critical value at 90 percent level
local T90 = 1.644853626951472
* Critical value at 68 percent level
local T68 = 0.994457883209753
*-------------------------------------------------------------------------------
* Section 3: Graph specifics
*-------------------------------------------------------------------------------
* Font size
local FS = 5
* Line width
local LW = 1.5
* Number of decimals in Tables
local FORMATT %4.3f
* Customized Blue Color
local LIGHT 222 235 247
local DARK 158 202 225
local SOLID 49 130 189
* Customized Red Color
local LIGHT2 254 224 210
local DARK2 252 146 114
local SOLID2 222 45 38
* Customized Green Color
local LIGHT3 229 245 224
local DARK3 161 217 155
local SOLID3 49 163 84
* Customized Orange Color
local LIGHT4 254 230 206
local DARK4 253 174 107
local SOLID4 230 85 13
*-------------------------------------------------------------------------------
* Section 4: Definition of variables
*-------------------------------------------------------------------------------
* Smooth transition function
gen p10 = .
gen p25 = .
gen p50 = .
gen p75 = .
gen p90 = .
gen desvest = .
gen promedio = .
levelsof code, local(cc)
foreach x in `cc' {
	quietly summarize debtgdp if code == `x' & quarter >= tq(1998q1), detail
    replace p10 = r(p10) if code == `x'
	replace p25 = r(p25) if code == `x'
	replace p50 = r(p50) if code == `x'
	replace p75 = r(p75) if code == `x'
	replace p90 = r(p90) if code == `x'
    replace desvest = r(sd) if code == `x'
	replace promedio = r(mean) if code == `x'
}
gen X = (L.debtgdp-promedio)/desvest
local theta 3
gen Fd = exp(`theta'*X)/(1+exp(`theta'*X))
gen Xc = L.debtgdp-L5.debtgdp
gen Fdc = exp(`theta'*Xc)/(1+exp(`theta'*Xc))
* Regimes
gen id_low = 0 // Low debt regime
replace id_low = (1-Fd)
gen id_high = 0 // High debt regime
replace id_high = Fd
gen id_neg = 0 // Negative shocks
replace id_neg = 1 if shock<0
gen id_pos = 0 // Positive shocks
replace id_pos = 1-id_neg
gen lowneg = id_low*id_neg
gen highneg = id_high*id_neg
gen lowpos = id_low*id_pos
gen highpos = id_high*id_pos
* Oil Supply News Shock
gen s_ = shock
gen sln_ = s_*id_low*id_neg
gen shn_ = s_*id_high*id_neg
gen slp_ = s_*id_low*id_pos
gen shp_ = s_*id_high*id_pos
label variable sln_ "Low Debt + shock<0"
label variable shn_ "Hight Debt + shock<0"
label variable slp_ "Low Debt + shock>0"
label variable shp_ "Hight Debt + shock>0"
* Real Oil Price
gen p_ = wti
label variable p_ "Crude Oil Price"
* Total Population
gen o_ = pop/1000000
label variable o_ "Population"
* Sovereign Default Risk
gen r_ = embi
label variable r_ "Sovereign Default Risk"
* GDP
gen g_ = ln(gdp2010_100/o_)*100
label variable g_ "Gross Domestic Product"
* Investment
gen i_ = ln(fbk100/o_)*100
label variable i_ "Investment"
* Private Consumption
gen c_  = ln(ch100/o_)*100
label variable c_ "Private Consumption"
* Public Consumption
gen cg_ = ln(cg100/o_)*100
label variable cg_ "Public Consumption"
* Trade Balance
gen tb_ = tradegdp
label variable tb_ "Net Exports/GDP"
* Real Exchange Rate
gen rer_ = ln(rer)*100
label variable rer_ "Real Exchange Rate"
* Public Spending
gen gg_ = gggdp
label variable gg_ "Public Spending/GDP"
* Fiscal Revenues
gen rev_ = revgdp
label variable rev_ "Fiscal Revenues/GDP"
* Primary Balance
gen pb_ = revgdp-gggdp
label variable pb_ "Primary Balance/GDP"
* Public Debt
gen d_ = debtgdp
label variable d_ "Public Debt/GDP"
* Bank Credit (GDP)
gen crg_ = creditgdp
label variable crg_ "Bank Credit/GDP"
* Banking Spread
gen spr_ = (tactive-tpasive)*100
label variable spr_ "Private Default Risk"
* GVA Oil
gen oil_ = ln(vaboil/o_)*100
label variable oil_ "GVA Oil Sector"
* GVA NonOil
gen nonoil_ = ln(vabnonoil/o_)*100
label variable nonoil_ "GVA Non-Oil Sector"
* Share of Tradable Sector
gen shr_ = shrt
label variable shr_ "Share of Tradable Sector"
* Unemployment
gen u_  = unem
label variable u_ "Unemployment Rate"	
* Consumer Price Index
gen cpi_ = ln(cpi)*100
label variable cpi_ "Consumer Price Index"
* Relative Prices
gen relp_ = ln(cpiusa/cpi)*100
label variable relp_ "Relative Consumer Prices"
* Reserves/GDP
gen res_ = (reserves-L4.reserves)*100/L4.reserves
label variable res_ "Change in Reserves"
* Current Account/GDP
gen ca_ = cagdp
label variable ca_ "Current Account/GDP"
* Exports/GDP
gen exp_ = exportgdp
label variable exp_ "Exports/GDP"
* Import/GDP
gen imp_ = importgdp
label variable imp_ "Imports/GDP"				
*-------------------------------------------------------------------------------
* Log file to save output
cd "$dir/Results"
log using "Debt.log", text replace
*-------------------------------------------------------------------------------
* Section 5: Local projection analysis
*-------------------------------------------------------------------------------
* List of endogenous variables 
local varendo rer_ pb_ shr_ oil_ c_ i_ tb_ d_ g_ u_ r_ spr_ crg_ ca_ gg_ res_
* Loop over variables
foreach var of local varendo {
	* Endogenous variable
	gen dy_ = d.`var'
	gen dyln_ = dy_*id_low*id_neg
	gen dyhn_ = dy_*id_high*id_neg
	gen dylp_ = dy_*id_low*id_pos
	gen dyhp_ = dy_*id_high*id_pos
	quietly forvalues h = 0/`HOR' {
		gen y_`h' = f`h'.`var' - l.`var'
		label variable y_`h' "`h'"
	}
	*---------------------------------------------------------------------------
	* Full sample
	*---------------------------------------------------------------------------
	eststo clear
	cap drop bl ul90 ll90 ul68 ll68 bh uh90 lh90 uh68 lh68 q zeros blc ulc90 llc90 ulc68 llc68
	gen bl=.
	gen ul90=.
	gen ll90=.
	gen ul68=.
	gen ll68=.
	gen bh=.
	gen uh90=.
	gen lh90=.
	gen uh68=.
	gen lh68=.	
	gen q=.
	gen zeros=.
	gen blc=. 
	gen ulc90=. 
	gen llc90=. 
	gen ulc68=. 
	gen llc68=.	
	* Loop over horizons
	quietly forvalues h = 0/`HOR' {	
		xtscc y_`h' sln_ shn_ slp_ shp_ lowneg highneg lowpos highpos L(1/`LAG').(dyln_ sln_ dyhn_ shn_ dylp_ slp_ dyhp_ shp_) F`h'.(dum1-dum11) i.code if quarter >= tq(1998q1),lag(`h') noconstant
		replace bl = `SIGN'*_b[sln_] if _n == `h'+1
		replace ul90 = `SIGN'*_b[sln_] + `T90'*_se[sln_]  if _n == `h'+1
		replace ll90 = `SIGN'*_b[sln_] - `T90'*_se[sln_]  if _n == `h'+1
		replace ul68 = `SIGN'*_b[sln_] + `T68'*_se[sln_]  if _n == `h'+1
		replace ll68 = `SIGN'*_b[sln_] - `T68'*_se[sln_]  if _n == `h'+1
		replace bh = `SIGN'*_b[shn_] if _n == `h'+1
		replace uh90 = `SIGN'*_b[shn_] + `T90'*_se[shn_]  if _n == `h'+1
		replace lh90 = `SIGN'*_b[shn_] - `T90'*_se[shn_]  if _n == `h'+1
		replace uh68 = `SIGN'*_b[shn_] + `T68'*_se[shn_]  if _n == `h'+1
		replace lh68 = `SIGN'*_b[shn_] - `T68'*_se[shn_]  if _n == `h'+1
		lincom -shn_+sln_
		replace blc = r(estimate) if _n == `h'+1
		replace ulc90 = r(estimate) + `T90'*r(se)  if _n == `h'+1
		replace llc90 = r(estimate) - `T90'*r(se)  if _n == `h'+1
		replace ulc68 = r(estimate) + `T68'*r(se)  if _n == `h'+1
		replace llc68 = r(estimate) - `T68'*r(se)  if _n == `h'+1 
		replace q = `h'	if _n == `h'+1
		replace zeros = 0 if _n == `h'+1	
		quietly eststo
	}
	local varlab : variable label `var'
	nois esttab, se nocons keep(sln_ shn_) ti("`varlab' - Full Sample") label star(* 0.32 ** 0.10) t(3) nogaps b(`FORMATT')
	*---------------------------------------------------------------------------
	* Graph - Low Debt Regime
	*---------------------------------------------------------------------------
	if "`var'" == "r_" | "`var'" == "spr_" {
	twoway ///
		(rarea ll90 ul90 q, fcolor("`LIGHT3'") lwidth(none)) ///
		(rarea ll68 ul68 q, fcolor("`DARK3'") lwidth(none)) ///
		(line bl q, color("`SOLID3'") lw(`LW') xla(0(1)`HOR') xtick(0(1)`HOR')) ///		
		(line zeros q, color(black) lw(0.5) xla(0(1)`HOR') xtick(0(1)`HOR')) ///				
		, ///
		ytitle( , size(`FS')) ///
		ylabel( , angle(horizontal) format(%9.0f) labsize(`FS')) ///
		xtitle("Quarters" , size(`FS')) ///
		xlabel( , labsize(`FS') grid) ///
		graphregion(color(white)) plotregion(color(white)) ///
		xsize(8) ///
		ysize(6) ///
		legend(off)
	}
	else if "`var'"== "g_" | "`var'" == "c_" | "`var'" == "i_" | "`var'" == "rer_" | "`var'" == "oil_" | "`var'" == "cg_" | "`var'" == "nonoil_" | "`var'" == "res_" {
	twoway ///
		(rarea ll90 ul90 q, fcolor("`LIGHT3'") lwidth(none)) ///
		(rarea ll68 ul68 q, fcolor("`DARK3'") lwidth(none)) ///
		(line bl q, color("`SOLID3'") lw(`LW') xla(0(1)`HOR') xtick(0(1)`HOR')) ///		
		(line zeros q, color(black) lw(0.5) xla(0(1)`HOR') xtick(0(1)`HOR')) ///				
		, ///
		ytitle( , size(`FS')) ///
		ylabel( , angle(horizontal) format(%9.2f) labsize(`FS')) ///
		xtitle("Quarters" , size(`FS')) ///
		xlabel( , labsize(`FS') grid) ///
		graphregion(color(white)) plotregion(color(white)) ///
		xsize(8) ///
		ysize(6) ///
		legend(off)	
	}
	else {
	twoway ///
		(rarea ll90 ul90 q, fcolor("`LIGHT3'") lwidth(none)) ///
		(rarea ll68 ul68 q, fcolor("`DARK3'") lwidth(none)) ///
		(line bl q, color("`SOLID3'") lw(`LW') xla(0(1)`HOR') xtick(0(1)`HOR')) ///	
		(line zeros q, color(black) lw(0.5) xla(0(1)`HOR') xtick(0(1)`HOR')) ///			
		, ///
		ytitle( , size(`FS')) ///
		ylabel( , angle(horizontal) format(%9.2f) labsize(`FS')) ///
		xtitle("Quarters" , size(`FS')) ///
		xlabel( , labsize(`FS') grid) ///
		graphregion(color(white)) plotregion(color(white)) ///
		xsize(8) ///
		ysize(6) ///
		legend(off)		
	}
	* Save graph file
	graph save "$dir/Results/Figures/GPH/`var'low_n.gph", replace	
	graph export "$dir/Results/Figures/PDF/`var'low_n.pdf", replace
	*---------------------------------------------------------------------------
	* Graph - High Debt Regime
	*---------------------------------------------------------------------------
	if "`var'" == "r_" | "`var'" == "spr_" {
	twoway ///
		(rarea lh90 uh90 q, fcolor("`LIGHT'") lwidth(none)) ///
		(rarea lh68 uh68 q, fcolor("`DARK'") lwidth(none)) ///
		(line bh q, color("`SOLID'") lw(`LW') xla(0(1)`HOR') xtick(0(1)`HOR')) ///		
		(line zeros q, color(black) lw(0.5) xla(0(1)`HOR') xtick(0(1)`HOR')) ///			
		, ///
		ytitle("Basis Points", size(`FS')) ///
		ylabel( , angle(horizontal) format(%9.0f) labsize(`FS')) ///
		xtitle("Quarters" , size(`FS')) ///
		xlabel( , labsize(`FS') grid) ///
		graphregion(color(white)) plotregion(color(white)) ///
		xsize(8) ///
		ysize(6) ///
		legend(off)
	}
	else if "`var'"== "g_" | "`var'" == "c_" | "`var'" == "i_" | "`var'" == "rer_" | "`var'" == "oil_" | "`var'" == "cg_" | "`var'" == "nonoil_" | "`var'" == "res_" {
	twoway ///
		(rarea lh90 uh90 q, fcolor("`LIGHT'") lwidth(none)) ///
		(rarea lh68 uh68 q, fcolor("`DARK'") lwidth(none)) ///
		(line bh q, color("`SOLID'") lw(`LW') xla(0(1)`HOR') xtick(0(1)`HOR')) ///			
		(line zeros q, color(black) lw(0.5) xla(0(1)`HOR') xtick(0(1)`HOR')) ///			
		, ///
		ytitle("Percentage Change", size(`FS')) ///
		ylabel( , angle(horizontal) format(%9.2f) labsize(`FS')) ///
		xtitle("Quarters" , size(`FS')) ///
		xlabel( , labsize(`FS') grid) ///
		graphregion(color(white)) plotregion(color(white)) ///
		xsize(8) ///
		ysize(6) ///
		legend(off)	
	}
	else {
	twoway ///
		(rarea lh90 uh90 q, fcolor("`LIGHT'") lwidth(none)) ///
		(rarea lh68 uh68 q, fcolor("`DARK'") lwidth(none)) ///
		(line bh q, color("`SOLID'") lw(`LW') xla(0(1)`HOR') xtick(0(1)`HOR')) ///		
		(line zeros q, color(black) lw(0.5) xla(0(1)`HOR') xtick(0(1)`HOR')) ///			
		, ///
		ytitle("Percentage Points", size(`FS')) ///
		ylabel( , angle(horizontal) format(%9.2f) labsize(`FS')) ///
		xtitle("Quarters" , size(`FS')) ///
		xlabel( , labsize(`FS') grid) ///	
		graphregion(color(white)) plotregion(color(white)) ///
		xsize(8) ///
		ysize(6) ///
		legend(off)		
	}
	* Save graph file
	graph save "$dir/Results/Figures/GPH/`var'high_n.gph", replace	
	graph export "$dir/Results/Figures/PDF/`var'high_n.pdf", replace
	*---------------------------------------------------------------------------
	* Graph - test of parameters
	*---------------------------------------------------------------------------
	if "`var'" == "r_" | "`var'" == "spr_" {
	twoway ///
		(rcap llc90 ulc90 q, lcolor(orange) lwidth(1.2)) ///
		(rcap llc68 ulc68 q, lcolor(orange_red) lwidth(1.2)) ///
		(line zeros q, color(black) lw(0.5) xla(0(1)12) xtick(0(1)12)) ///
		(scatter blc q, msymbol(O) mcolor(black) msize(medium)), ///
		ytitle(, size(`FS')) ///
		ylabel( , angle(horizontal) format(%9.0f) labsize(`FS')) ///
		xtitle("Quarters" , size(`FS')) ///
		xlabel( , labsize(`FS') grid) ///
 		graphregion(color(white)) plotregion(color(white)) ///
		xsize(8) ///
		ysize(6) ///
		legend(order(1 "90% CI" 2 "68% CI") ///
        pos(5) ring(0) col(1) ///
        size(medium) region(lstyle(none)))
	}
	else if "`var'"== "g_" | "`var'" == "c_" | "`var'" == "i_" | "`var'" == "rer_" | "`var'" == "oil_" | "`var'" == "cg_" | "`var'" == "nonoil_" | "`var'" == "res_" {
	twoway ///
		(rcap llc90 ulc90 q, lcolor(orange) lwidth(1.2)) ///
		(rcap llc68 ulc68 q, lcolor(orange_red) lwidth(1.2)) ///
		(line zeros q, color(black) lw(0.5) xla(0(1)12) xtick(0(1)12)) ///
		(scatter blc q, msymbol(O) mcolor(black) msize(medium)), ///
		ytitle(, size(`FS')) ///
		ylabel( , angle(horizontal) format(%9.2f) labsize(`FS')) ///
		xtitle("Quarters" , size(`FS')) ///
		xlabel( , labsize(`FS') grid) ///
 		graphregion(color(white)) plotregion(color(white)) ///
		xsize(8) ///
		ysize(6) ///
		legend(order(1 "90% CI" 2 "68% CI") ///
        pos(5) ring(0) col(1) ///
        size(medium) region(lstyle(none)))
	}
	else {
	twoway ///
		(rcap llc90 ulc90 q, lcolor(orange) lwidth(1.2)) ///
		(rcap llc68 ulc68 q, lcolor(orange_red) lwidth(1.2)) ///
		(line zeros q, color(black) lw(0.5) xla(0(1)12) xtick(0(1)12)) ///
		(scatter blc q, msymbol(O) mcolor(black) msize(medium)), ///
		ytitle(, size(`FS')) ///
		ylabel( , angle(horizontal) format(%9.2f) labsize(`FS')) ///
		xtitle("Quarters" , size(`FS')) ///
		xlabel( , labsize(`FS') grid) ///
 		graphregion(color(white)) plotregion(color(white)) ///
		xsize(8) ///
		ysize(6) ///
		legend(order(1 "90% CI" 2 "68% CI") ///
        pos(5) ring(0) col(1) ///
        size(medium) region(lstyle(none)))
	}
	* Save graph file
	graph save "$dir/Results/Figures/GPH/`var'test_n.gph", replace	
	graph export "$dir/Results/Figures/PDF/`var'test_n.pdf", replace		
	drop dyln_ dyhn_ dylp_ dyhp_ dy_ y_*
}
*-------------------------------------------------------------------------------
* Close log file
log close
set graph on			
*-------------------------------------------------------------------------------
